
import System.Random

------------------------- Exercise 1

repeatMe :: IO ()
repeatMe = do
    x <- getLine
    putStr "You just told me:"
    putStrLn x


------------------------- Exercise 2

lizzy :: IO ()
lizzy = do
    putStrLn welcome
    lizzyLoop

lizzyLoop :: IO ()
lizzyLoop = do
    str <- getLine
    if str == "Exit"
    then do
        putStrLn exit
    else do
        r <- randomIO
        putStrLn (response str r)
        lizzyLoop


------------------------- Exercise 3



-------------------------

welcome :: String
welcome =
  "\nDr. Lizzy -- Good morning, how are you today. Please tell me what's on your mind.\n"

exit :: String
exit =
  "\nDr. Lizzy -- Thank you for coming in today. I think we made good progress. I will see you next week at the same time.\n"

response :: String -> Int -> String
response str r =
  "\nDr. Lizzy -- " ++ x ++ y ++ "\n"
     where
       x = responses1 !! (r `mod` 3)
       y = responses2 !! (r `mod` 5)
       responses1 =
         ["Interesting that you say \"" ++ str ++ "\"\n"
         ,"Hmm... "
         ,"Let\'s examine that more closely, shall we.\n"
         ]
       responses2 =
         ["Please tell me more about that."
         ,"How does that make you feel?"
         ,"Now, why do you mention that?"
         ,"Do you think this has something to do with your mother?"
         ,"Go on."
         ]




------------------------- Lambda-calculus

type Var = String

data Term =
    Variable Var
  | Lambda   Var  Term
  | Apply    Term Term

example :: Term
example = Lambda "a" (Lambda "x" (Apply (Apply (Lambda "y" (Apply (Variable "a") (Variable "c"))) (Variable "x")) (Variable "b")))

pretty :: Term -> String
pretty = f 0
    where
      f i (Variable x) = x
      f i (Lambda x m) = if i /= 0 then "(" ++ s ++ ")" else s where s = "\\" ++ x ++ ". " ++ f 0 m 
      f i (Apply  n m) = if i == 2 then "(" ++ s ++ ")" else s where s = f 1 n ++ " " ++ f 2 m

instance Show Term where
  show = pretty

n1 :: Term
n1 = Lambda "x" (Variable "x")

n2 :: Term
n2 = Lambda "x" (Apply (Lambda "y" (Variable "x")) (Variable "z"))

n3 :: Term
n3 = Apply (Lambda "x" (Lambda "y" (Apply (Variable "x") (Variable "y")))) n1

-------------------------

merge :: Ord a => [a] -> [a] -> [a]
merge xs [] = xs
merge [] ys = ys
merge (x:xs) (y:ys)
    | x == y    = x : merge xs ys
    | x <= y    = x : merge xs (y:ys)
    | otherwise = y : merge (x:xs) ys

minus :: Ord a => [a] -> [a] -> [a]
minus xs [] = xs
minus [] ys = []
minus (x:xs) (y:ys)
    | x <  y    = x : minus    xs (y:ys)
    | x == y    =     minus    xs    ys
    | otherwise =     minus (x:xs)   ys

-------------------------

used :: Term -> [Var]
used (Variable x) = [x]
used (Lambda x n) = [x] `merge` used n
used (Apply  n m) = used n `merge` used m

free :: Term -> [Var]
free (Variable x) = [x]
free (Lambda x n) = free n `minus` [x]
free (Apply  n m) = free n `merge` free m


-------------------------

numeral :: Int -> Term
numeral i = Lambda "f" (Lambda "x" (numeral' i))
  where
    numeral' i
      | i <= 0    = Variable "x"
      | otherwise = Apply (Variable "f") (numeral' (i-1))


-------------------------

variables :: [Var]
variables = [ [x] | x <- ['a'..'z'] ] ++ [ x : show i | i <- [1..] , x <- ['a'..'z'] ]

removeAll :: [Var] -> [Var] -> [Var]
removeAll xs ys = [ x | x <- xs , not (elem x ys) ]

fresh :: [Var] -> Var
fresh = head . removeAll variables


rename :: Var -> Var -> Term -> Term
rename x y (Variable z)
    | z == x    = Variable y
    | otherwise = Variable z
rename x y (Lambda z n)
    | z == x    = Lambda z n
    | otherwise = Lambda z (rename x y n)
rename x y (Apply n m) = Apply (rename x y n) (rename x y m)


substitute :: Var -> Term -> Term -> Term
substitute x n (Variable y)
    | x == y    = n
    | otherwise = Variable y
substitute x n (Lambda y m)
    | x == y    = Lambda y m
    | otherwise = Lambda z (substitute x n (rename y z m))
    where z = fresh (used n `merge` used m `merge` [x,y])
substitute x n (Apply m p) = Apply (substitute x n m) (substitute x n p)


------------------------- Exercise 4


beta :: Term -> [Term]
beta (Variable x) = []
beta (Apply (Lambda x m) n) = [substitute x n m] ++ [Apply (Lambda x m) n' | n' <- beta n] ++ [Apply (Lambda x m') n | m' <- beta m]
beta (Lambda x m) = [Lambda x m' | m' <- beta m]
beta (Apply m n) = [Apply m' n | m' <- beta m] ++ [Apply m n' | n' <- beta n]

normalize :: Term -> IO ()
normalize x = do
    putStrLn (show x)
    let x' = beta x
    if length(x') == 0
    then do
        putStrLn "Finished"
    else do
        normalize (head x')



